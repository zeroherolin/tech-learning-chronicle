# LLVM & MLIR

Code: [https://github.com/llvm/llvm-project](https://github.com/llvm/llvm-project) \
Clang Webpage: [https://clang.llvm.org/](https://clang.llvm.org/) \
MLIR Webpage: [https://mlir.llvm.org/](https://mlir.llvm.org/) \
Discourse Webpage: [https://discourse.llvm.org/tag/mlir](https://discourse.llvm.org/tag/mlir)

## æ„å»ºMLIRç¯å¢ƒ

```bash
conda create -n mlir python=3.12
conda activate mlir

git clone https://github.com/llvm/llvm-project.git

cd llvm-project

python -m pip install --upgrade pip
python -m pip install -r mlir/python/requirements.txt

mkdir build && cd build

cmake -G Ninja ../llvm \
    -DLLVM_ENABLE_PROJECTS="mlir;clang" \
    -DLLVM_BUILD_EXAMPLES=OFF \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_ENABLE_ASSERTIONS=ON \
    -DLLVM_INSTALL_UTILS=ON \
    -DMLIR_ENABLE_BINDINGS_PYTHON=ON \
    -DPython3_EXECUTABLE=`which python3`

ninja

sudo nano ~/.bashrc
## æ·»åŠ 
export LLVM_BUILD_DIR=~/llvm-project/build
export PATH=$LLVM_BUILD_DIR/bin:$PATH
export PYTHONPATH=$LLVM_BUILD_DIR/tools/mlir/python_packages/mlir_core
```

é€šè¿‡pybindæ˜¯ä¸ºä»¥ååœ¨pythonä¸­è°ƒç”¨åšå‡†å¤‡

## MLIR Opt æµ‹è¯•

- æŸ¥çœ‹æ”¯æŒçš„pass

Passes [https://mlir.llvm.org/docs/Passes/](https://mlir.llvm.org/docs/Passes/)

- mliræºç 

simple_complex.mlir

```mlir
module {
    // å£°æ˜å¤–éƒ¨å‡½æ•°
    llvm.func @printf(!llvm.ptr, ...) -> i32
    llvm.func @atof(!llvm.ptr) -> f64

    // å®šä¹‰è¾“å‡ºæ ¼å¼å­—ç¬¦ä¸²
    llvm.mlir.global internal constant @fmt_str("%f %f\n\00")

    func.func @main( // int main(int argc, char* argv[])
        %argc: i32, %argv: !llvm.ptr
    ) -> i32 {
        // å®šä¹‰ç´¢å¼•å¸¸é‡
        %c1 = arith.constant 1 : i64
        %c2 = arith.constant 2 : i64
        %c3 = arith.constant 3 : i64
        %c4 = arith.constant 4 : i64

        // è·å–argvå…ƒç´ åœ°å€ï¼ˆchar**çš„ç´¢å¼•è®¿é—®ï¼‰
        %argv1_ptr = llvm.getelementptr %argv[%c1] : (!llvm.ptr, i64) -> !llvm.ptr, i64
        %argv2_ptr = llvm.getelementptr %argv[%c2] : (!llvm.ptr, i64) -> !llvm.ptr, i64
        %argv3_ptr = llvm.getelementptr %argv[%c3] : (!llvm.ptr, i64) -> !llvm.ptr, i64
        %argv4_ptr = llvm.getelementptr %argv[%c4] : (!llvm.ptr, i64) -> !llvm.ptr, i64

        // åŠ è½½å®é™…å­—ç¬¦ä¸²æŒ‡é’ˆï¼ˆchar*ï¼‰
        %argv1 = llvm.load %argv1_ptr : !llvm.ptr -> !llvm.ptr
        %argv2 = llvm.load %argv2_ptr : !llvm.ptr -> !llvm.ptr
        %argv3 = llvm.load %argv3_ptr : !llvm.ptr -> !llvm.ptr
        %argv4 = llvm.load %argv4_ptr : !llvm.ptr -> !llvm.ptr

        // è½¬æ¢å­—ç¬¦ä¸²ä¸ºæµ®ç‚¹æ•°
        %re1_f64 = llvm.call @atof(%argv1) : (!llvm.ptr) -> f64
        %im1_f64 = llvm.call @atof(%argv2) : (!llvm.ptr) -> f64
        %re2_f64 = llvm.call @atof(%argv3) : (!llvm.ptr) -> f64
        %im2_f64 = llvm.call @atof(%argv4) : (!llvm.ptr) -> f64

        // è½¬æ¢ä¸ºf32
        %re1 = arith.truncf %re1_f64 : f64 to f32
        %im1 = arith.truncf %im1_f64 : f64 to f32
        %re2 = arith.truncf %re2_f64 : f64 to f32
        %im2 = arith.truncf %im2_f64 : f64 to f32

        // åˆ›å»ºå¤æ•°
        %cplx1 = complex.create %re1, %im1 : complex<f32>
        %cplx2 = complex.create %re2, %im2 : complex<f32>

        // å¤æ•°åŠ æ³•
        %result = complex.add %cplx1, %cplx2 : complex<f32>

        // æå–å®éƒ¨å’Œè™šéƒ¨
        %real = complex.re %result : complex<f32>
        %imag = complex.im %result : complex<f32>

        // è½¬æ¢ä¸ºf64ï¼šprintfçš„%fæ ¼å¼å¿…é¡»64ä½åŒç²¾åº¦æµ®ç‚¹æ•°
        %real64 = arith.extf %real : f32 to f64
        %imag64 = arith.extf %imag : f32 to f64

        // è°ƒç”¨printfè¾“å‡ºç»“æœ
        %fmt = llvm.mlir.addressof @fmt_str : !llvm.ptr
        llvm.call @printf(%fmt, %real64, %imag64) vararg(!llvm.func<i32 (ptr, ...)>) : (!llvm.ptr, f64, f64) -> i32

        // è¿”å›0
        %zero = arith.constant 0 : i32
        func.return %zero : i32
    }
}
```

- æ‰§è¡ŒLowering Pass

```bash
mlir-opt simple_complex.mlir \
    --convert-complex-to-llvm \
    --convert-arith-to-llvm \
    --convert-func-to-llvm \
    -o simple_complex_llvm.mlir
```

- ç”Ÿæˆllvm ir

```bash
mlir-translate --mlir-to-llvmir simple_complex_llvm.mlir -o simple_complex.ll
```

- ç¼–è¯‘ç”Ÿæˆå¯æ‰§è¡Œæ–‡ä»¶

```bash
clang -O3 simple_complex.ll -o simple_complex -Wno-override-module
```

- å®Œæ•´è„šæœ¬ï¼ˆå«éªŒè¯ï¼‰

run_simple_complex.sh

```bash
#!/bin/bash

# ç¼–è¯‘é˜¶æ®µ
build() {
    mlir-opt simple_complex.mlir \
        --convert-complex-to-llvm \
        --convert-arith-to-llvm \
        --convert-func-to-llvm \
        -o simple_complex_llvm.mlir

    mlir-translate --mlir-to-llvmir simple_complex_llvm.mlir -o simple_complex.ll

    clang -O3 simple_complex.ll -o simple_complex -Wno-override-module
}

# æ‰§è¡Œé˜¶æ®µ
run() {
    local re1=$1
    local im1=$2
    local re2=$3
    local im2=$4

    ./simple_complex "$re1" "$im1" "$re2" "$im2"
}

# ä¸»æµç¨‹
build && run "$1" "$2" "$3" "$4"
```

è¾“å‡º

> $ ./run_simple_complex.sh 1.5 2.3 3.7 4.1 \
5.200000 6.400000

## MLIR EmitC æµ‹è¯•

ç›¸å…³pass

```
-convert-arith-to-emitc
-convert-func-to-emitc
-convert-math-to-emitc
-convert-memref-to-emitc
-convert-scf-to-emitc
-convert-to-emitc
```

- mliræºç 

vector_add.mlir

```mlir
module {
  func.func @vector_add(%arg0: memref<10xf32>, %arg1: memref<10xf32>, %arg2: memref<10xf32>) {
    %c0 = arith.constant 0 : index
    %c10 = arith.constant 10 : index
    %c1 = arith.constant 1 : index
    scf.for %i = %c0 to %c10 step %c1 {
      %a = memref.load %arg0[%i] : memref<10xf32>
      %b = memref.load %arg1[%i] : memref<10xf32>
      %sum = arith.addf %a, %b : f32
      memref.store %sum, %arg2[%i] : memref<10xf32>
    }
    return
  }
}

```

- Passåˆ°emitcæ–¹è¨€

```bash
mlir-opt vector_add.mlir \
    --convert-arith-to-emitc \
    --convert-func-to-emitc \
    --convert-math-to-emitc \
    --convert-memref-to-emitc \
    --convert-scf-to-emitc \
    -o vector_add_emitc.mlir
```

è¾“å‡º

vector_add_emitc.mlir

```mlir
module {
  emitc.func @vector_add(%arg0: memref<10xf32>, %arg1: memref<10xf32>, %arg2: memref<10xf32>) {
    %0 = builtin.unrealized_conversion_cast %arg2 : memref<10xf32> to !emitc.array<10xf32>
    %1 = builtin.unrealized_conversion_cast %arg1 : memref<10xf32> to !emitc.array<10xf32>
    %2 = builtin.unrealized_conversion_cast %arg0 : memref<10xf32> to !emitc.array<10xf32>
    %3 = "emitc.constant"() <{value = 0 : index}> : () -> !emitc.size_t
    %4 = "emitc.constant"() <{value = 10 : index}> : () -> !emitc.size_t
    %5 = "emitc.constant"() <{value = 1 : index}> : () -> !emitc.size_t
    for %arg3 = %3 to %4 step %5  : !emitc.size_t {
      %6 = builtin.unrealized_conversion_cast %arg3 : !emitc.size_t to index
      %7 = subscript %2[%6] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      %8 = load %7 : <f32>
      %9 = subscript %1[%6] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      %10 = load %9 : <f32>
      %11 = add %8, %10 : (f32, f32) -> f32
      %12 = subscript %0[%6] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      assign %11 : f32 to %12 : <f32>
    }
    return
  }
}
```

è¿™é‡Œå®˜æ–¹passæœªå®Œå…¨å®ç°ï¼Œæ‰‹åŠ¨è°ƒæ•´å¦‚ä¸‹

vector_add_emitc.mlir

```mlir
module {
  emitc.func @vector_add(%arg0: !emitc.array<10xf32>, %arg1: !emitc.array<10xf32>, %arg2: !emitc.array<10xf32>) {
    %3 = "emitc.constant"() <{value = 0 : index}> : () -> index
    %4 = "emitc.constant"() <{value = 10 : index}> : () -> index
    %5 = "emitc.constant"() <{value = 1 : index}> : () -> index
    for %arg3 = %3 to %4 step %5 {
      %7 = subscript %arg0[%arg3] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      %8 = load %7 : <f32>
      %9 = subscript %arg1[%arg3] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      %10 = load %9 : <f32>
      %11 = add %8, %10 : (f32, f32) -> f32
      %12 = subscript %arg2[%arg3] : (!emitc.array<10xf32>, index) -> !emitc.lvalue<f32>
      assign %11 : f32 to %12 : <f32>
    }
    return
  }
}
```

- EmitC

```bash
mlir-translate vector_add_emitc.mlir -mlir-to-cpp -o vector_add.cpp
```

è¾“å‡º

vector_add.cpp

```cpp
void vector_add(float v1[10], float v2[10], float v3[10]) {
  size_t v4 = 0;
  size_t v5 = 10;
  size_t v6 = 1;
  for (size_t v7 = v4; v7 < v5; v7 += v6) {
    float v8 = v1[v7];
    float v9 = v2[v7];
    float v10 = v8 + v9;
    v3[v7] = v10;
  }
  return;
}
```

emitc-translate[https://github.com/iml130/mlir-emitc]å·²åˆå¹¶è‡³mlir-translate

***
ğŸ”™ [Go Back](README.md)
